@ filename: int_director.s
@
@ Interrupt handler for UART4, GPIO1_31 button, eventually a timer
@
@ Written by: James Ross

.text
.global _int_director
_int_director:
@ Interupt definitions
.equ INTC_BASE,           0x48200000 @ INCPS               	  Base Address
.equ INTC_CONTROL,        0x48       @ INTC_CONTROL        	  register offset
.equ INTC_CONFIG,         0x10       @ INTC_SYSCONFIG      	  register offset
.equ INTC_MIR_CLEAR2,     0xC8       @ INTC_MIR_CLEAR2     	  register offset
.equ INTC_MIR_CLEAR3,     0xE8       @ INTC_MIR_CLEAR3     	  register offset
.equ INTC_MIR_SET3,       0xEC       @ INTC_MIR_SET3       	  register offset
.equ INTC_PENDING_IRQ1,	  0xB8		 @ INTC_PENDING_IRQ1	  register offset
.equ INTC_PENDING_IRQ3,   0xF8       @ INTC_PENDING_IRQ2      register offset
.equ GPIOINT1A,           0x4        @ GPIOINT1A in MIR1/IPI3 Mask

@ GPIO definitions
.equ GPIO1_BASE,		  0x4804C000  @ GPIO1 				Base Address
.equ GPIO_IRQ_STAT_0,	  0x2C	  	  @ GPIO_IRQSTATUS_0 	register offset
.equ GPIO_31,			  0x80000000  @ GPIO_31 raised bit	Mask

@ UART definitions
.equ UART4_BASE,    	  0x481A8000 @ UART4          	      Base Address
.equ UART_TXHR,	   		  0x0	  	 @ THR Transmit Holding   register offset
.equ UART_LSR,			  0x14		 @ Line control reg		  register offset
.equ UART_MODEMCTRL,      0x10 	     @ Modem CTRL 			  register offset
.equ UART_MSR,			  0x18		 @ Modem Status Reg 	  register offset
.equ UART_IIR,     		  0x8	 	 @ Interrupt ID		  	  register offset
.equ UART_IER,     	      0x4	 	 @ IER_UART, Int EN       register offset
.equ CTS_CHECK, 		  0x10		 @ Bit 4 used in MSR	  Mask
.equ THR_CHECK,			  0x20       @ Bit 5 used in LSR	  Mask
.equ UART4INT,			  0x2000     @ UART4INT in MIR1/IPI1  Mask
.equ THR_CLR,			  0xFFFD 	 @ And Clears bit1 in IER Mask
.equ SET_UART_IER,  	  0x000A     @ THRIT,MODEMSTIT 16bit  Val

.equ DELIM,				  0x0D		 @ Final character sent in a messege

@ register assignment definitions
intcBase  .req R10
gpio1Base .req R9
uart4Base .req R8

@******************************* start _int_director ***************************
	STMFD SP!, {R0-R12, LR}
	LDR intcBase, =INTC_BASE
	LDR uart4Base, =UART4_BASE
	
UART4_IRQ_TST:
	@ Check to see if UART4 triggered the interrupt
	LDR R3, [intcBase, #INTC_PENDING_IRQ1]
	TST R3, #UART4INT
	BEQ BTN_IRQ_TST @ else check button press
	
	@ Check IIR_UART to see if one of the UART interrupts occured
	LDR R3, [uart4Base, #UART_IIR]
	TST R3, #0x1
	BNE END_SVC

TALKER_SVC:
	@ See if CTS# is asserted
	LDR R3, [uart4Base, #UART_MSR]
	TST R3, #CTS_CHECK
	BNE YES_CTS_THR_TST
	@ else continue NO_CTS_THR_TST

NO_CTS_THR_TST:  @ CTS# not asserted low
	@ Check if THR is empty
	LDR R3, [uart4Base, #UART_LSR]
	TST R3, #THR_CHECK
	BEQ END_SVC  @ CTS# not asserted, THR not empty, return
	
	@ THR is empty, CTS# not asserted, Mask THR interrupt to 0
	LDR R3, [uart4Base, #UART_IER]
	LDR R4, =THR_CLR
	AND R4, R4, R3		@ Clear bit, THR interrupt
	STR R4, [uart4Base, #UART_IER] @ Store result with lowest bit (THR INT) clear
	
	@ Return to wait loop waiting for CTS# to be asserted
	B END_SVC
	
YES_CTS_THR_TST: @ CTS# asserted low
	@ Check if THR is empty
	LDR R3, [uart4Base, #UART_LSR]
	TST R3, #THR_CHECK
	BEQ END_SVC  @ THR not empty, return to wait for THR
	
	LDR R3, [uart4Base, #UART_IER]
	LDR R4, =THR_CLR
	AND R4, R4, R3				   @ Clear bit, THR interrupt
	STR R4, [uart4Base, #UART_IER] @ Store result with lowest bit (THR INT) clear
	@ else continue to SEND_CHAR	
	
SEND_CHAR:

	LDR R3, =CHAR_PTR
   	LDR R4, [R3]
    LDRB R5, [R4], #1			     @ Get character to send, increment addr
    STRB R5, [uart4Base, #UART_TXHR] @ Send char on the UART
    STR R4, [R3]					 @ Store incremented ptr position

	@ Check to see if we are on the last character
	CMP R5, #DELIM
	BEQ RESET_UART_VALUES
	BEQ END_SVC
	
	@ Set interrupt to generate is CTS# changes state and if Transmit Holding 
	@ Regiser (THR) is empty IF more characters need to be sent.
	@MOV R4, #SET_UART_IER
	@STR R4, [uart4Base, #UART_IER]
	B END_SVC
	
RESET_UART_VALUES:
	STMFD SP!, {R0-R12, LR}
	
	@ Reset pointer location to start of message
	LDR R3, =CHAR_PTR
	LDR R4, =MESSAGE
	STR R4, [R3]
	
	@ disbale UART interrupts, enabled on next button press
	MOVEQ R4, #0x0
	STREQ R4, [uart4Base, #UART_IER]
	
	LDMFD SP!, {R0-R12, LR} @ go back to int procedure
	
BTN_IRQ_TST:
	@ Check if INT occured from GPIO1
	LDR R3, [intcBase, #INTC_PENDING_IRQ3]
	TST R3, #GPIOINT1A
	BEQ END_SVC
	
	@ Check if the button on GPIO_31 triggered the interrupt
	LDR gpio1Base, =GPIO1_BASE
	LDR R3, [gpio1Base, #GPIO_IRQ_STAT_0]
	TST R3, #GPIO_31
	BEQ END_SVC @ If GPIO_31 does not have a pending INT, return
	
SVC_BUTTON:
	@ ***********************************************************************
	@ TODO: Set a flag or use a value that prevents the button being pressed
	@		while the message is being sent to be spoken
	@ **********************************************************************

	@ Disable GPIO1_31 intrrupt requests and INTC interrupt requests
	MOV R3, #GPIO_31	
	STR R3, [gpio1Base, #GPIO_IRQ_STAT_0] @ 1 at bit 31 of GPIO_IRQ_STAT_0

	@ TODO: Pretty sure this should be only one on the button press. Disabled
	@ 		upon the last message being sent, so next button press will set it
	@		again.
	@ Set interrupt to generate is CTS# changes state and if Transmit Holding 
	@ Regiser (THR) is empty
	MOV R4, #SET_UART_IER
	STR R4, [uart4Base, #UART_IER]
	B END_SVC	
	
END_SVC:
	@ Disable NEWIRQA bit so processor can respond to IRQ
	MOV R3, #0x1
	STR R3, [intcBase, #INTC_CONTROL]	

	LDMFD SP!, {R0-R12, LR}
	SUBS PC, LR, #0x4
.end
@****************** EOF ****************